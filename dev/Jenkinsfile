def runDevPipeline() {
    node {
        try {
            // Khai báo biến môi trường trực tiếp trong hàm để đảm bảo scope
            def NAMESPACE = "dev"
            def DOCKER_REGISTRY = 'csc11007'
            def APP_NAME = "petclinic-${NAMESPACE}"
            def GITOPS_REPO = "https://github.com/csc11007-assignments/spring-pet-clinic-microservices-configuration.git"
            def VALUES_FILE = "charts/dev/values.yaml"

            stage('Checkout source code repository') {
                checkout([$class: 'GitSCM',
                    branches: [[name: env.BRANCH_NAME]],
                    userRemoteConfigs: [[url: 'https://github.com/csc11007-assignments/spring-petclinic-microservice-ex2.git', credentialsId: 'github-token']]
                ])
            }

            stage('Get commit hash') {
                env.COMMIT = sh(script: 'git rev-parse --short HEAD', returnStdout: true).trim()
                echo "Commit hash: ${env.COMMIT}"
            }

            stage('Build & Push docker images') {
                def serviceMap = [
                    'config-server': '8888',
                    'discovery-server': '8761',
                    'customers-service': '8081',
                    'visits-service': '8082',
                    'vets-service': '8083',
                    'genai-service': '8084',
                    'api-gateway': '8080',
                    'admin-server': '9090'
                ]

                // Sử dụng docker.withRegistry để đăng nhập Docker Hub
                docker.withRegistry('https://index.docker.io/v1/', 'csc11007') {
                    serviceMap.each { service, port ->
                        def imageName = "${DOCKER_REGISTRY}/spring-petclinic-${service}:dev-${env.COMMIT}"
                        echo "Building Docker image for ${service} → ${imageName}"

                        sh """
                            docker build \
                              --build-arg SERVICE_NAME=${service} \
                              --build-arg EXPOSED_PORT=${port} \
                              -t ${imageName} .
                        """
                        sh "docker push ${imageName}"
                    }
                }
            }

            stage('Checkout GitOps configuration repository') {
                dir('gitops-repo') {
                    withCredentials([usernamePassword(
                        credentialsId: 'github-token',
                        usernameVariable: 'GIT_USERNAME',
                        passwordVariable: 'GIT_PASSWORD'
                    )]) {
                        sh """
                            git clone https://${GIT_USERNAME}:${GIT_PASSWORD}@github.com/csc11007-assignments/spring-pet-clinic-microservices-configuration.git .
                            git config user.email "jenkins@example.com"
                            git config user.name "Jenkins CI"
                        """
                    }
                }
            }

            stage('Update configuration with new image tags') {
                dir('gitops-repo') {
                    def tag = "dev-${env.COMMIT}"
                    def services = [
                        'config-server',
                        'customers-service',
                        'discovery-server',
                        'visits-service',
                        'vets-service',
                        'genai-service',
                        'api-gateway',
                        'admin-server'
                    ]

                    services.each { svc ->
                        sh """
                            if grep -q "^[[:space:]]*${svc}:" ${VALUES_FILE}; then
                                lineNumber=\$(grep -n "^[[:space:]]*${svc}:" ${VALUES_FILE} | cut -d':' -f1)
                                if [ ! -z "\$lineNumber" ]; then
                                    tagLine=\$((lineNumber + 3))
                                    sed -i "\${tagLine}s/tag: .*/tag: ${tag}/" ${VALUES_FILE}
                                fi
                            fi
                        """
                    }

                    sh "cat ${VALUES_FILE}"
                }
            }

            stage('Commit and Push to GitOps Repository') {
                dir('gitops-repo') {
                    withCredentials([usernamePassword(
                        credentialsId: 'github-token',
                        usernameVariable: 'GIT_USERNAME',
                        passwordVariable: 'GIT_PASSWORD'
                    )]) {
                        sh """
                            git add ${VALUES_FILE}
                            git commit -m "Update image tags for services to dev-${env.COMMIT}" || echo "No changes to commit"
                            git push origin main
                        """
                    }
                }
            }
        } catch (Exception e) {
            echo "Error in dev pipeline: ${e.message}"
            throw e // Ném lỗi để pipeline đánh dấu thất bại
        } finally {
            cleanWs()
            echo "Dev pipeline completed"
        }
    }
}

return this